allprojects {
    group 'dev.xdark.asm-inline'
    version '1.0-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    repositories {
        mavenCentral()
    }

    configurations {
        shade
        compileOnly.extendsFrom shade
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    project.ext {
        asm = 'org.ow2.asm:asm:9.0'
    }

    publishing {
        repositories {
            maven {
                url = 'https://repo.c7x.ru/repository/maven-snapshots'
                credentials {
                    username = System.getenv("CRI_REPO_LOGIN") ?: project.properties["CRI_REPO_LOGIN"] as String
                    password = System.getenv("CRI_REPO_PASSWORD") ?: project.properties["CRI_REPO_PASSWORD"] as String
                }
            }
        }
        publications {
            maven(MavenPublication) {
                groupId project.group
                artifactId project.name
                version project.version
                from components.java
            }
        }
    }
}
